plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

android {
    namespace "com.bluebubbles.messaging"
    compileSdk 35

    lintOptions {
        checkReleaseBuilds false
        disable 'InvalidPackage'
    }

    aaptOptions {
        noCompress "tflite"
    }

    defaultConfig {
        applicationId "com.bluebubbles.messaging"
        minSdkVersion 24
        targetSdkVersion 34
        versionCode 20002000 + flutterVersionCode.toInteger()
        versionName flutterVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    kotlinOptions {
        jvmTarget = "21"
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
        debug {

        }
    }

    flavorDimensions = ["app"]

    productFlavors {
        joel {
            dimension "app"
            resValue "string", "app_name_en", "BlueBubbles Dev (Joel)"
            resValue "color", "ic_launcher_background", "#4c49de"
            resValue "string", "file_provider", "com.bluebubbles.messaging.joel.fileprovider"
            applicationId "com.bluebubbles.messaging.joel"
        }
        tanay {
            dimension "app"
            resValue "string", "app_name_en", "BlueBubbles (Tanay Special Sauce)"
            resValue "color", "ic_launcher_background", "#4c49de"
            resValue "string", "file_provider", "com.bluebubbles.messaging.tanay.fileprovider"
            applicationId "com.bluebubbles.messaging.tanay"
        }
        alpha {
            dimension "app"
            resValue "string", "app_name_en", "OpenBubbles (Alpha)"
            resValue "color", "ic_launcher_background", "#49dbde"
            resValue "string", "file_provider", "com.bluebubbles.messaging.alpha.fileprovider"
            applicationId "com.bluebubbles.messaging.alpha"
        }
        beta {
            dimension "app"
            resValue "string", "app_name_en", "OpenBubbles (Beta)"
            resValue "color", "ic_launcher_background", "#4990de"
            resValue "string", "file_provider", "com.bluebubbles.messaging.beta.fileprovider"
            applicationId "com.bluebubbles.messaging.beta"
        }
        prod {
            getIsDefault().set(true)
            dimension "app"
            resValue "string", "app_name_en", "OpenBubbles"
            resValue "color", "ic_launcher_background", "#4990de"
            resValue "string", "file_provider", "com.openbubbles.messaging.fileprovider"
            applicationId "com.openbubbles.messaging"
        }
    }

    buildFeatures {
        aidl true
        viewBinding true
    }

    buildTypes {
        release {
            productFlavors.joel.signingConfig signingConfigs.debug
            productFlavors.tanay.signingConfig signingConfigs.debug
            productFlavors.alpha.signingConfig signingConfigs.debug
            productFlavors.beta.signingConfig signingConfigs.release
            productFlavors.prod.signingConfig signingConfigs.release
            productFlavors.alpha.signingConfig signingConfigs.release
            minifyEnabled false
            shrinkResources false
        }
    }
    sourceSets {
        main {
            java {
                srcDirs 'src/main/java', 'src/main/java/workers'
            }
        }
    }
}

flutter {
    source '../..'
}

// For Cloud Firestore okio error
configurations.all {
    resolutionStrategy {
        force 'com.squareup.okhttp:okhttp:2.7.5'
        force 'com.squareup.okio:okio:1.17.5'
    }
}

dependencies {
    // Android native functions
    implementation "androidx.core:core-ktx:1.13.1"
    implementation "androidx.sharetarget:sharetarget:1.2.0"
    implementation 'androidx.browser:browser:1.8.0'
    implementation 'androidx.activity:activity-ktx:1.9.1'
    implementation "androidx.work:work-runtime:2.9.0"
    implementation "androidx.concurrent:concurrent-futures-ktx:1.2.0"

    // Firebase items
    implementation 'com.google.firebase:firebase-messaging:24.0.0'
    implementation 'com.google.firebase:firebase-database:21.0.0'
    implementation 'com.google.firebase:firebase-messaging-directboot:24.0.0'
    implementation 'com.google.firebase:firebase-iid:21.1.0'
    implementation 'com.google.firebase:firebase-firestore:25.0.0'

    // Kotlin Coroutines (async)
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.8.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.8.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.8.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-guava:1.8.1"

    // Socket IO
    implementation ('io.socket:socket.io-client:2.1.1') {
        // excluding org.json which is provided by Android
        exclude group: 'org.json', module: 'json'
    }

    // JSON parsing
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'

    // For AGP 7.4+ desugaring
    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs:2.0.4")

    // Unified Push
    implementation 'org.unifiedpush.android:connector:2.5.0'

    // uniffi JNA dependency
    implementation "net.java.dev.jna:jna:5.12.0@aar"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-guava:1.6.0"
}
